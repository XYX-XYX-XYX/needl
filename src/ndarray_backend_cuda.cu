#include <cstdint>
#include <cuda_runtime.h>
#include <pybind11/numpy.h>
#include <pybind11/pybind11.h>
#include <pybind11/stl.h>

#include <iostream>
#include <sstream>

namespace needle {
namespace cuda {

#define BASE_THREAD_NUM 256

#define TILE 4
typedef float scalar_t;
const size_t ELEM_SIZE = sizeof(scalar_t);

struct CudaArray {
  CudaArray(const size_t size) {
    cudaError_t err = cudaMalloc(&ptr, size * ELEM_SIZE);
    if (err != cudaSuccess) throw std::runtime_error(cudaGetErrorString(err));
    this->size = size;
  }
  ~CudaArray() { cudaFree(ptr); }
  size_t ptr_as_int() { return (size_t)ptr; }
  
  scalar_t* ptr;
  size_t size;
};

struct CudaDims {
  dim3 block, grid;
};

CudaDims CudaOneDim(size_t size) {
  /**
   * Utility function to get cuda dimensions for 1D call
   */
  CudaDims dim;
  size_t num_blocks = (size + BASE_THREAD_NUM - 1) / BASE_THREAD_NUM;
  dim.block = dim3(BASE_THREAD_NUM, 1, 1);
  dim.grid = dim3(num_blocks, 1, 1);
  return dim;
}

#define MAX_VEC_SIZE 8
struct CudaVec {
  uint32_t size;
  int32_t data[MAX_VEC_SIZE];
};

CudaVec VecToCuda(const std::vector<int32_t>& x) {
  CudaVec shape;
  if (x.size() > MAX_VEC_SIZE) throw std::runtime_error("Exceeded CUDA supported max dimesions");
  shape.size = x.size();
  for (size_t i = 0; i < x.size(); i++) {
    shape.data[i] = x[i];
  }
  return shape;
}

////////////////////////////////////////////////////////////////////////////////
// Fill call
////////////////////////////////////////////////////////////////////////////////

__global__ void FillKernel(scalar_t* out, scalar_t val, size_t size) {
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if (gid < size) out[gid] = val;
}

void Fill(CudaArray* out, scalar_t val) {
  CudaDims dim = CudaOneDim(out->size);
  FillKernel<<<dim.grid, dim.block>>>(out->ptr, val, out->size);
}

////////////////////////////////////////////////////////////////////////////////
// Compact and setitem cals
////////////////////////////////////////////////////////////////////////////////

// Untility function to convert contiguous index i to memory location from strides
__device__ static CudaVec GetIndices(CudaVec shape, size_t gid) 
{

  CudaVec indices;
  indices.size = shape.size;
  int idx = indices.size - 1;
  for(; idx >= 0; idx--) {
    indices.data[idx] = gid % shape.data[idx];
    gid = gid / shape.data[idx];
  }
  return indices;

}


__global__ void CompactKernel(const scalar_t* a, scalar_t* out, size_t size, CudaVec shape,
                              CudaVec strides, size_t offset) {
  /**
   * The CUDA kernel for the compact opeation.  This should effectively map a single entry in the 
   * non-compact input a, to the corresponding item (at location gid) in the compact array out.
   * 
   * Args:
   *   a: CUDA pointer to a array
   *   out: CUDA point to out array
   *   size: size of out array
   *   shape: vector of shapes of a and out arrays (of type CudaVec, for past passing to CUDA kernel)
   *   strides: vector of strides of out array
   *   offset: offset of out array
   */
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if(gid >= size) return;
  /// BEGIN SOLUTION
  auto out_ptr = out + gid;
  CudaVec indices = GetIndices(shape, gid);
  int32_t stride = 0;
  for(size_t i = 0; i < indices.size; i++) {
    stride += indices.data[i] * strides.data[i];
  }
  *out_ptr = a[offset + stride];
  /// END SOLUTION
}

void Compact(const CudaArray& a, CudaArray* out, std::vector<int32_t> shape,
             std::vector<int32_t> strides, size_t offset) {
  /**
   * Compact an array in memory.  Unlike the C++ version, in CUDA this will primarily call the 
   * relevant CUDA kernel.  In this case, we illustrate how you should set this up (i.e., we give 
   * you the code for this fuction, and also the prototype for the CompactKernel() function).  For
   * the functions after this, however, you'll need to define these kernels as you see fit to 
   * execute the underlying function.
   * 
   * Args:
   *   a: non-compact represntation of the array, given as input
   *   out: compact version of the array to be written
   *   shape: shapes of each dimension for a and out
   *   strides: strides of the *a* array (not out, which has compact strides)
   *   offset: offset of the *a* array (not out, which has zero offset, being compact)
   */

  // Nothing needs to be added here
  CudaDims dim = CudaOneDim(out->size);
  CompactKernel<<<dim.grid, dim.block>>>(a.ptr, out->ptr, out->size, VecToCuda(shape),
                                         VecToCuda(strides), offset);
}


__global__ void EwiseSetitemKernel(const scalar_t* a, scalar_t* out, size_t size, CudaVec shape,
                              CudaVec strides, size_t offset) 
{

  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if(gid >= size) return;
  auto out_ptr = out + offset;
  auto a_ptr = a + gid;
  CudaVec indices = GetIndices(shape, gid);
  int32_t stride = 0;
  for(size_t i = 0; i < indices.size; i++) {
    stride += indices.data[i] * strides.data[i];
  }
  out_ptr[stride] = *a_ptr;
}

void EwiseSetitem(const CudaArray& a, CudaArray* out, std::vector<int32_t> shape,
                  std::vector<int32_t> strides, size_t offset) {
  /**
   * Set items in a (non-compact) array using CUDA.  Yyou will most likely want to implement a
   * EwiseSetitemKernel() function, similar to those above, that will do the actual work.
   * 
   * Args:
   *   a: _compact_ array whose items will be written to out
   *   out: non-compact array whose items are to be written
   *   shape: shapes of each dimension for a and out
   *   strides: strides of the *out* array (not a, which has compact strides)
   *   offset: offset of the *out* array (not a, which has zero offset, being compact)
   */
  /// BEGIN SOLUTION
  CudaDims dim = CudaOneDim(a.size);
  EwiseSetitemKernel<<<dim.grid, dim.block>>>(a.ptr, out->ptr, a.size, VecToCuda(shape),
                                         VecToCuda(strides), offset);
  /// END SOLUTION
}

__global__ void ScalarSetitemKernel(scalar_t val, scalar_t* out, size_t size, CudaVec shape,
                              CudaVec strides, size_t offset) 
{

  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if(gid >= size) return;
  auto out_ptr = out + offset;
  CudaVec indices = GetIndices(shape, gid);
  int32_t stride = 0;
  for(size_t i = 0; i < indices.size; i++) {
    stride += indices.data[i] * strides.data[i];
  }
  out_ptr[stride] = val;
}

void ScalarSetitem(size_t size, scalar_t val, CudaArray* out, std::vector<int32_t> shape,
                   std::vector<int32_t> strides, size_t offset) {
  /**
   * Set items is a (non-compact) array
   * 
   * Args:
   *   size: number of elements to write in out array (note that this will note be the same as
   *         out.size, because out is a non-compact subset array);  it _will_ be the same as the 
   *         product of items in shape, but covenient to just pass it here.
   *   val: scalar value to write to
   *   out: non-compact array whose items are to be written
   *   shape: shapes of each dimension of out
   *   strides: strides of the out array
   *   offset: offset of the out array
   */
  /// BEGIN SOLUTION
  CudaDims dim = CudaOneDim(size);
  ScalarSetitemKernel<<<dim.grid, dim.block>>>(val, out->ptr, size, VecToCuda(shape),
                                         VecToCuda(strides), offset);
  /// END SOLUTION
}

////////////////////////////////////////////////////////////////////////////////
// Elementwise and scalar operations
////////////////////////////////////////////////////////////////////////////////


__global__ void EwiseAddKernel(const scalar_t* a, const scalar_t* b, scalar_t* out, size_t size) {
  // Calculate the global index of the thread.
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if (gid < size) out[gid] = a[gid] + b[gid];
}

void EwiseAdd(const CudaArray& a, const CudaArray& b, CudaArray* out) {
  /**
   * Add together two CUDA arrays.
   * Args:
   *   a: Input array 'a' to be added
   *   b: Input array 'b' to be added
   *   out: Output array to store the result of 'a + b'
   */
  CudaDims dim = CudaOneDim(out->size);

  // Kernel will execute on 'dim.grid' blocks, each containing 'dim.block' threads.
  EwiseAddKernel<<<dim.grid, dim.block>>>(a.ptr, b.ptr, out->ptr, out->size);
}

__global__ void ScalarAddKernel(const scalar_t* a, scalar_t val, scalar_t* out, size_t size) {
  // Calculate the global index of the thread.
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if (gid < size) out[gid] = a[gid] + val;
}

void ScalarAdd(const CudaArray& a, scalar_t val, CudaArray* out) {
  /**
   * Add a scalar value to every element of a CUDA array.
   * Args:
   *   a: Input array 'a'
   *   val: Scalar value to be added
   *   out: Output array to store the result of 'a + val'
   */
  CudaDims dim = CudaOneDim(out->size);

  // Launch the ScalarAddKernel that will add the scalar 'val' to each element of array 'a', 
  // and store the result in array 'out'.
  ScalarAddKernel<<<dim.grid, dim.block>>>(a.ptr, val, out->ptr, out->size);
}

/**
 * In the code the follows, use the above template to create analogous elementise
 * and and scalar operators for the following functions.  See the numpy backend for
 * examples of how they should work.
 *   - EwiseMul, ScalarMul
 *   - EwiseDiv, ScalarDiv
 *   - ScalarPower
 *   - EwiseMaximum, ScalarMaximum
 *   - EwiseEq, ScalarEq
 *   - EwiseGe, ScalarGe
 *   - EwiseLog
 *   - EwiseExp
 *   - EwiseTanh
 *
 * If you implement all these naively, there will be a lot of repeated code, so
 * you are welcome (but not required), to use macros or templates to define these
 * functions (however you want to do so, as long as the functions match the proper)
 * signatures above.
 */
 
template <typename op> 
__global__ void EwiseKernel(const scalar_t *a, const scalar_t *b, scalar_t *out, size_t size, op operation)
{
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if (gid < size) out[gid] = operation(a[gid], b[gid]);
}
struct MulOp { __device__ scalar_t operator()(scalar_t a, scalar_t b) const { return a * b; } };
struct DivOp { __device__ scalar_t operator()(scalar_t a, scalar_t b) const { return a / b; } };
struct MaxOp { __device__ scalar_t operator()(scalar_t a, scalar_t b) const { return max(a, b); } };
struct EqOp { __device__ scalar_t operator()(scalar_t a, scalar_t b) const { return a == b; } };
struct GeOp { __device__ scalar_t operator()(scalar_t a, scalar_t b) const { return a >= b; } };
struct PowerOp { __device__ scalar_t operator()(scalar_t a, scalar_t b) const { return pow(a, b); } };
struct LogOp { __device__ scalar_t operator()(scalar_t a) const { return log(a); } };
struct ExpOp { __device__ scalar_t operator()(scalar_t a) const { return exp(a); } };
struct TanhOp { __device__ scalar_t operator()(scalar_t a) const { return tanh(a); } };

void EwiseMul(const CudaArray &a, const CudaArray &b, CudaArray *out)
{
  CudaDims dim = CudaOneDim(out->size);
  EwiseKernel<<<dim.grid, dim.block>>>(a.ptr, b.ptr, out->ptr, out->size, MulOp());
}

void EwiseDiv(const CudaArray &a, const CudaArray &b, CudaArray *out)
{
  CudaDims dim = CudaOneDim(out->size);
  EwiseKernel<<<dim.grid, dim.block>>>(a.ptr, b.ptr, out->ptr, out->size, DivOp());
}

void EwiseMaximum(const CudaArray &a, const CudaArray &b, CudaArray *out)
{
  CudaDims dim = CudaOneDim(out->size);
  EwiseKernel<<<dim.grid, dim.block>>>(a.ptr, b.ptr, out->ptr, out->size, MaxOp());
}

void EwiseEq(const CudaArray &a, const CudaArray &b, CudaArray *out)
{
  CudaDims dim = CudaOneDim(out->size);
  EwiseKernel<<<dim.grid, dim.block>>>(a.ptr, b.ptr, out->ptr, out->size, EqOp());
} 

void EwiseGe(const CudaArray &a, const CudaArray &b, CudaArray *out)
{
  CudaDims dim = CudaOneDim(out->size);
  EwiseKernel<<<dim.grid, dim.block>>>(a.ptr, b.ptr, out->ptr, out->size, GeOp());
} 

template <typename op>
__global__ void ScalarKernel(const scalar_t *a, const scalar_t val, scalar_t *out, size_t size, op operation)
{
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if(gid < size) out[gid] = operation(a[gid], val);
}

void ScalarMul(const CudaArray &a, const scalar_t val, CudaArray *out)
{
  CudaDims dim = CudaOneDim(out->size);
  ScalarKernel<<<dim.grid, dim.block>>>(a.ptr, val, out->ptr, out->size, MulOp());
}

void ScalarDiv(const CudaArray &a, const scalar_t val, CudaArray *out)
{
  CudaDims dim = CudaOneDim(out->size);
  ScalarKernel<<<dim.grid, dim.block>>>(a.ptr, val, out->ptr, out->size, DivOp());
}

void ScalarPower(const CudaArray &a, const scalar_t val, CudaArray *out)
{
  CudaDims dim = CudaOneDim(out->size);
  ScalarKernel<<<dim.grid, dim.block>>>(a.ptr, val, out->ptr, out->size, PowerOp());
}

void ScalarMaximum(const CudaArray &a, const scalar_t val, CudaArray *out)
{
  CudaDims dim = CudaOneDim(out->size);
  ScalarKernel<<<dim.grid, dim.block>>>(a.ptr, val, out->ptr, out->size, MaxOp());
}

void ScalarEq(const CudaArray &a, const scalar_t val, CudaArray *out)
{
  CudaDims dim = CudaOneDim(out->size);
  ScalarKernel<<<dim.grid, dim.block>>>(a.ptr, val, out->ptr, out->size, EqOp());
}

void ScalarGe(const CudaArray &a, const scalar_t val, CudaArray *out)
{
  CudaDims dim = CudaOneDim(out->size);
  ScalarKernel<<<dim.grid, dim.block>>>(a.ptr, val, out->ptr, out->size, GeOp());
}

template <typename op>
__global__ void UnaryKernel(const scalar_t *a, scalar_t *out, size_t size, op operation)
{
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if(gid < size) out[gid] = operation(a[gid]);
}

void EwiseLog(const CudaArray &a, CudaArray *out)
{
  CudaDims dim = CudaOneDim(out->size);
  UnaryKernel<<<dim.grid, dim.block>>>(a.ptr, out->ptr, out->size, LogOp());
}

void EwiseExp(const CudaArray &a, CudaArray *out)
{
  CudaDims dim = CudaOneDim(out->size);
  UnaryKernel<<<dim.grid, dim.block>>>(a.ptr, out->ptr, out->size, ExpOp());
}

void EwiseTanh(const CudaArray &a, CudaArray *out)
{
  CudaDims dim = CudaOneDim(out->size);
  UnaryKernel<<<dim.grid, dim.block>>>(a.ptr, out->ptr, out->size, TanhOp());
}
////////////////////////////////////////////////////////////////////////////////
// Elementwise and scalar operations
////////////////////////////////////////////////////////////////////////////////

__global__ void NaiveMatmulKernel(const scalar_t *a, const scalar_t *b, scalar_t *out, uint32_t M, uint32_t N, uint32_t P)
{
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if(gid >= M * P) return;
  uint32_t idx = gid % P;
  uint32_t idy = gid / P;

  scalar_t sum = 0;
  for(size_t i = 0; i < N; i++) {
    sum += a[idy * N + i] * b[i * P + idx];
  }
  out[idy * P + idx] = sum;
}

#define BLOCK_DIM 32

__global__ void ShareMemoryMatmulKernel(const scalar_t* a, const scalar_t* b, scalar_t* out, uint32_t M, uint32_t N, uint32_t P) 
{
  int tx = threadIdx.x;
  int ty = threadIdx.y;
  int bx = blockIdx.x;
  int by = blockIdx.y;

  int row = by * BLOCK_DIM + ty;
  int col = bx * BLOCK_DIM + tx;

  scalar_t sum = 0;

  __shared__ scalar_t As[BLOCK_DIM][BLOCK_DIM];
  __shared__ scalar_t Bs[BLOCK_DIM][BLOCK_DIM];

  for (int k = 0; k < (N + BLOCK_DIM - 1) / BLOCK_DIM; ++k) {
    if (row < M && k * BLOCK_DIM + tx < N)
      As[ty][tx] = a[row * N + k * BLOCK_DIM + tx];
    else
      As[ty][tx] = 0.0;

    if (col < P && k * BLOCK_DIM + ty < N)
      Bs[ty][tx] = b[(k * BLOCK_DIM + ty) * P + col];
    else
      Bs[ty][tx] = 0.0;

    __syncthreads();

    for (int n = 0; n < BLOCK_DIM; ++n)
      sum += As[ty][n] * Bs[n][tx];

    __syncthreads();
  }

  if (row < M && col < P)
    out[row * P + col] = sum;
}

#define THREAD_DIM 2


__global__ void Block2DMatmulKernel(const scalar_t* a, const scalar_t* b, scalar_t* out, uint32_t M, uint32_t N, uint32_t P)
{
  int bx = blockIdx.x;
  int by = blockIdx.y;
  int tidx = threadIdx.x;

  __shared__ scalar_t As[BLOCK_DIM][BLOCK_DIM];
  __shared__ scalar_t Bs[BLOCK_DIM][BLOCK_DIM];

  int block_row = by * BLOCK_DIM;
  int block_col = bx * BLOCK_DIM;

  int load_row = tidx / BLOCK_DIM;
  int load_col = tidx % BLOCK_DIM;

  int compute_row = tidx / (BLOCK_DIM / THREAD_DIM) * THREAD_DIM;
  int compute_col = tidx % (BLOCK_DIM / THREAD_DIM) * THREAD_DIM;

  a = a + block_row * N;
  b = b + block_col;
  out = out + block_row * P + block_col;
  
  scalar_t result[THREAD_DIM * THREAD_DIM] = {0.0};
  
  for(size_t k = 0; k < (N + BLOCK_DIM - 1) / BLOCK_DIM; k++) {
    //load As
    for(size_t i = 0; i < BLOCK_DIM * BLOCK_DIM / BASE_THREAD_NUM; i++) {
      int share_row = load_row + i * (BASE_THREAD_NUM / BLOCK_DIM);
      int share_col = load_col;
      if((block_row + share_row) < M && (k * BLOCK_DIM + share_col) < N)
        As[share_row][share_col] = a[share_row * N + share_col];
      else 
        As[share_row][share_col] = 0.0;
    }

    //load Bs
    for(size_t i = 0; i < BLOCK_DIM * BLOCK_DIM / BASE_THREAD_NUM; i++) {
      int share_row = load_row + i * (BASE_THREAD_NUM / BLOCK_DIM);
      int share_col = load_col;
      if((k * BLOCK_DIM + share_row) < N && (block_col + share_col) < P)
        Bs[share_row][share_col] = b[share_row * P + share_col];
      else 
        Bs[share_row][share_col] = 0.0;
    }
    
    __syncthreads();

    //compute
    for(int m = 0; m < BLOCK_DIM; m++) {
      for(int i = 0; i < THREAD_DIM; i++) {
        for(int j = 0; j < THREAD_DIM; j++) {
          result[i * THREAD_DIM + j] += As[compute_row + i][m] * Bs[m][compute_col + j];
        }
      }
    }
    __syncthreads();
    
    a += BLOCK_DIM;
    b += BLOCK_DIM * P;
  }

  for(int i = 0; i < THREAD_DIM; i++) {
    for(int j = 0; j < THREAD_DIM; j++) {
      if((compute_row + block_row + i) < M && (compute_col + block_col + j) < P) {
        out[(compute_row + i) * P + compute_col + j] = result[i * 2 + j];
      }
    }
  }
}

void Matmul(const CudaArray& a, const CudaArray& b, CudaArray* out, uint32_t M, uint32_t N,
            uint32_t P) {
  /**
   * Multiply two (compact) matrices into an output (also comapct) matrix.  You will want to look
   * at the lecture and notes on GPU-based linear algebra to see how to do this.  Since ultimately
   * mugrade is just evaluating correctness, you _can_ implement a version that simply parallelizes
   * over (i,j) entries in the output array.  However, to really get the full benefit of this
   * problem, we would encourage you to use cooperative fetching, shared memory register tiling, 
   * and other ideas covered in the class notes.  Note that unlike the tiled matmul function in
   * the CPU backend, here you should implement a single function that works across all size
   * matrices, whether or not they are a multiple of a tile size.  As with previous CUDA
   * implementations, this function here will largely just set up the kernel call, and you should
   * implement the logic in a separate MatmulKernel() call.
   * 
   *
   * Args:
   *   a: compact 2D array of size m x n
   *   b: comapct 2D array of size n x p
   *   out: compact 2D array of size m x p to write the output to
   *   M: rows of a / out
   *   N: columns of a / rows of b
   *   P: columns of b / out
   */

  /// BEGIN SOLUTION
  // CudaDims dim = CudaOneDim(out->size);
  // NaiveMatmulKernel<<<dim.grid, dim.block>>>(a.ptr, b.ptr, out->ptr, M, N, P);

  // dim3 block(BLOCK_DIM, BLOCK_DIM);
  // dim3 grid((P + BLOCK_DIM - 1) / BLOCK_DIM, (M + BLOCK_DIM - 1) / BLOCK_DIM);
  // ShareMemoryMatmulKernel<<<grid, block>>>(a.ptr, b.ptr, out->ptr, M, N, P);

  dim3 block(BASE_THREAD_NUM);
  dim3 grid((P + BLOCK_DIM - 1) / BLOCK_DIM, (M + BLOCK_DIM - 1) / BLOCK_DIM);
  Block2DMatmulKernel<<<grid, block>>>(a.ptr, b.ptr, out->ptr, M, N, P);
  /// END SOLUTION
}

////////////////////////////////////////////////////////////////////////////////
// Max and sum reductions
////////////////////////////////////////////////////////////////////////////////
__global__ void ReduceMaxKernel(const scalar_t *a, scalar_t *out, size_t reduce_size, size_t size)
{
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if(gid >= size) return;
  auto a_ptr = a + reduce_size * gid;
  auto out_ptr = out + gid;
  scalar_t max_num = a_ptr[0];
  for(size_t i = 0; i < reduce_size; i++) {
    max_num = max(max_num, a_ptr[i]);
  }

  *out_ptr = max_num;
}

void ReduceMax(const CudaArray& a, CudaArray* out, size_t reduce_size) {
  /**
   * Reduce by taking maximum over `reduce_size` contiguous blocks.  Even though it is inefficient,
   * for simplicity you can perform each reduction in a single CUDA thread.
   * 
   * Args:
   *   a: compact array of size a.size = out.size * reduce_size to reduce over
   *   out: compact array to write into
   *   redice_size: size of the dimension to reduce over
   */
  /// BEGIN SOLUTION
  CudaDims dim = CudaOneDim(out->size);
  
  ReduceMaxKernel<<<dim.grid, dim.block>>>(a.ptr, out->ptr, reduce_size, out->size);

  /// END SOLUTION
}

__global__ void ReduceSumKernel(const scalar_t *a, scalar_t *out, size_t reduce_size, size_t size)
{
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if(gid >= size) return;

  auto a_ptr = a + reduce_size * gid;
  auto out_ptr = out + gid;

  scalar_t sum = 0; 
  for(size_t i = 0; i < reduce_size; i++) {
    sum += a_ptr[i];
  }

  *out_ptr = sum;
}

void ReduceSum(const CudaArray& a, CudaArray* out, size_t reduce_size) {
  /**
   * Reduce by taking summation over `reduce_size` contiguous blocks.  Again, for simplicity you 
   * can perform each reduction in a single CUDA thread.
   * 
   * Args:
   *   a: compact array of size a.size = out.size * reduce_size to reduce over
   *   out: compact array to write into
   *   redice_size: size of the dimension to reduce over
   */
  /// BEGIN SOLUTION
  CudaDims dim = CudaOneDim(out->size);
  ReduceSumKernel<<<dim.grid, dim.block>>>(a.ptr, out->ptr, reduce_size, out->size);

  /// END SOLUTION
}

}  // namespace cuda
}  // namespace needle

PYBIND11_MODULE(ndarray_backend_cuda, m) {
  namespace py = pybind11;
  using namespace needle;
  using namespace cuda;

  m.attr("__device_name__") = "cuda";
  m.attr("__tile_size__") = TILE;

  py::class_<CudaArray>(m, "Array")
      .def(py::init<size_t>(), py::return_value_policy::take_ownership)
      .def_readonly("size", &CudaArray::size)
      .def("ptr", &CudaArray::ptr_as_int);

  // return numpy array, copying from CPU
  m.def("to_numpy", [](const CudaArray& a, std::vector<size_t> shape, std::vector<size_t> strides,
                       size_t offset) {
    std::vector<size_t> numpy_strides = strides;
    std::transform(numpy_strides.begin(), numpy_strides.end(), numpy_strides.begin(),
                   [](size_t& c) { return c * ELEM_SIZE; });

    // copy memory to host
    scalar_t* host_ptr = (scalar_t*)std::malloc(a.size * ELEM_SIZE);
    if (host_ptr == 0) throw std::bad_alloc();
    cudaError_t err = cudaMemcpy(host_ptr, a.ptr, a.size * ELEM_SIZE, cudaMemcpyDeviceToHost);
    if (err != cudaSuccess) throw std::runtime_error(cudaGetErrorString(err));

    // return numpy array
    py::capsule deallocate_buffer(host_ptr, [](void* p) { free(p); });
    return py::array_t<scalar_t>(shape, numpy_strides, host_ptr + offset, deallocate_buffer);
  });

  // copy numpy array to GPU
  m.def("from_numpy", [](py::array_t<scalar_t> a, CudaArray* out) {
    cudaError_t err =
        cudaMemcpy(out->ptr, a.request().ptr, out->size * ELEM_SIZE, cudaMemcpyHostToDevice);
    if (err != cudaSuccess) throw std::runtime_error(cudaGetErrorString(err));
  });

  m.def("fill", Fill);
  m.def("compact", Compact);
  m.def("ewise_setitem", EwiseSetitem);
  m.def("scalar_setitem", ScalarSetitem);
  m.def("ewise_add", EwiseAdd);
  m.def("scalar_add", ScalarAdd);

  m.def("ewise_mul", EwiseMul);
  m.def("scalar_mul", ScalarMul);
  m.def("ewise_div", EwiseDiv);
  m.def("scalar_div", ScalarDiv);
  m.def("scalar_power", ScalarPower);

  m.def("ewise_maximum", EwiseMaximum);
  m.def("scalar_maximum", ScalarMaximum);
  m.def("ewise_eq", EwiseEq);
  m.def("scalar_eq", ScalarEq);
  m.def("ewise_ge", EwiseGe);
  m.def("scalar_ge", ScalarGe);

  m.def("ewise_log", EwiseLog);
  m.def("ewise_exp", EwiseExp);
  m.def("ewise_tanh", EwiseTanh);

  m.def("matmul", Matmul);

  m.def("reduce_max", ReduceMax);
  m.def("reduce_sum", ReduceSum);
}
